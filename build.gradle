import org.gradle.internal.jvm.Jvm

buildscript {
    ext {
        // SDK
        sdkCompileVersion = 33
        sdkMinVersion = 24
        sdkTargetVersion = 33

        // Android jetpack
        androidxArchCore = "2.2.0"
        androidxLifecycle = "2.5.1"
        androidxRoom = "2.5.1"
        androidxCore = "1.9.0"
        androidxFragment = "1.5.5"
        androidxAppcompat = "1.5.1"
        androidxLegacy = "1.0.0"

        // Kotlin
        orgJetbrainsKotlin = "1.8.21"
        orgJetbrainsKotlinx = "1.6.4"

        // Koin
        ioInsertKoin = "3.3.3"

        // Markwon
        markwon = "4.6.2"

        // Moshi
        comSquareupMoshi = '1.14.0'

        // Testing
        ioMockk = "1.13.3"
        junitVersion = "4.13.2"
        androidxTestExt = "1.1.5"
        androidxTest = "1.4.0"
        androidxTestEspresso = "3.5.1"
        androidxTestUiautomator = "2.2.0"
        androidxAnnotation = "1.6.0"
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$orgJetbrainsKotlin"
        classpath 'com.google.gms:google-services:4.3.14'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.9.2'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.1.0"
    }
}

plugins {
    id "org.sonarqube" version "4.3.0.3225"
    id 'com.google.devtools.ksp' version '1.8.22-1.0.11' apply false
}

println "Gradle uses Java ${Jvm.current()}"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url 'https://artifactory.mxtracks.info/artifactory/gradle-dev-local/'
        }
    }
}

subprojects {
    apply plugin: "com.google.devtools.ksp"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.sonarqube"
}

sonarqube {
    properties {
        property "sonar.projectKey", "owncloud_android"
        property "sonar.organization", "owncloud-1"
        property "sonar.projectVersion", getBranchName()
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def getBranchName() {
    def name = "git rev-parse --abbrev-ref HEAD".execute()
    return name.text.toString().trim()
}

import org.gradle.internal.jvm.Jvm

buildscript {
    ext {
        // SDK
        sdkCompileVersion = 33
        sdkMinVersion = 24
        sdkTargetVersion = 33
    }

    repositories {
        google()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.4.2"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$orgJetbrainsKotlin"
        classpath 'com.google.gms:google-services:4.3.10'
        classpath 'com.google.firebase:firebase-crashlytics-gradle:2.8.1'
        classpath "org.jlleitschuh.gradle:ktlint-gradle:11.1.0"
    }
}

plugins {
    alias libs.plugins.sonarqube
    alias libs.plugins.ksp apply false
}

println "Gradle uses Java ${Jvm.current()}"

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
        maven {
            url 'https://artifactory.mxtracks.info/artifactory/gradle-dev-local/'
        }
    }
}

subprojects {
    apply plugin: "com.google.devtools.ksp"
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    apply plugin: "org.sonarqube"
}

sonarqube {
    properties {
        property "sonar.projectKey", "owncloud_android"
        property "sonar.organization", "owncloud-1"
        property "sonar.projectVersion", getBranchName()
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

def getBranchName() {
    def name = "git rev-parse --abbrev-ref HEAD".execute()
    return name.text.toString().trim()
}
